<<Grammar Name="nLess">>

//******************************************** Less
^Parse:  primary / FATAL<"Less files should start with comments, declerations, or rulesets.">;
^^primary: (comment/ declaration/ ruleset)+ / FATAL<"primary should be comments, declerations, or rulesets.">;
^^comment: ws '/*' (!'*/' . )* '*/' ws / ws '//' (![\n] .)* [\n] ws;
^^declaration:  standard_declaration / catchall_declaration ;
^^standard_declaration: ws (ident / variable)  s ':' s expressions  s (';'/ ws &'}') ws ;
^^catchall_declaration:  ws ident s ':' s ';' ws ;
^^ident: ('*'/'-'/[-a-z0-9_]+);
^^variable: '@' [-a-zA-Z0-9_]+; 
^^expressions: operation_expressions / space_delimited_expressions / [-a-zA-Z0-9_%*/.&=:,#+? \[\]()]+ ;
^^operation_expressions:  expression (operator expression)+;
^^space_delimited_expressions: expression (WS expression)* important? ;
^^ important:      s '!' s 'important' ;
^^expression: '(' s expressions s ')' / entity ;
^^operator: S [-+*/] S / [-+*/] ;
^^ruleset : standard_ruleset / mixin_ruleset;
^^standard_ruleset: ws selectors [{] ws primary ws [}] ws;
^^mixin_ruleset :  ws selectors ';' ws; 
^^selectors :  ws selector (s ',' ws selector)* ws ;
^^selector : (s select element s)+ arguments? ;
^^arguments : '(' s argument s (',' s argument s)* ')';
^^argument : color / number unit / string / [a-zA-Z]+ '=' dimension / [-a-zA-Z0-9_%$/.&=:;#+?]+ / function / keyword (S keyword)*;
^element : (class_id / tag / ident) attribute* ('(' ident? attribute* ')')? / attribute+ / '@media' / '@font-face';
^class_id : tag? (class / id)+;
^attribute :  '[' tag ([|~*$^]? '=') (tag / string) ']' / '[' (tag / string) ']';
^class:  '.' [_a-zA-Z] [-a-zA-Z0-9_]*;
^id: '#' [_a-zA-Z] [-a-zA-Z0-9_]*;
^tag : [a-zA-Z] [-a-zA-Z]* [0-9]? / '*';
^^select : (s [+>~] s / '::' / s ':' / S)?;
^^function: function_name arguments ;
^^function_name: [-a-zA-Z_]+;

//******************************************** Entity
^^entity :  function / fonts / keyword  / variable / literal ; //accessor missing
^^fonts : font (s ',' s font)+  ;
^^font: [a-zA-Z] [-a-zA-Z0-9]* ;
^^literal: color / (dimension / [-a-z]+) '/' dimension / number unit / string ;
^^keyword: [-a-zA-Z]+ !ns;
^^string: ['] (!['] . )* ['] / ["] (!["] . )* ["] ;
^^dimension: number WS unit;
^^number: '-'? [0-9]* '.' [0-9]+ / '-'? [0-9]+;
^^unit: ('px'/'em'/'pc'/'%'/'ex'/'s'/'pt'/'cm'/'mm')?;
^^color: '#' rgb;
^rgb:(rgb_node)(rgb_node)(rgb_node);
^rgb_node : hex hex;
hex: [a-fA-F0-9];

//******************************************** Common
WS: [ \r\n]+;
ws: [ \r\n]*;
s:  [ ]*;
S:  [ ]+;
ns: ![ ;\n] .;
<</Grammar>>